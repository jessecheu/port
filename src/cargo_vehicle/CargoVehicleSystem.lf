/**
 * The main menu for the Cargo Vehicle built upon the Pololu 3pi+ rp2040 Robot. 
 * Allows to switch the drive mode, stop mode, and other modes.
 */
 target C {
    platform: {
      name: "rp2040",
      board: "pololu_3pi_2040_robot"
    },
    threading: false,
  }
  
  import Line from "../lib/Line.lf"
  import Display from "../lib/Display.lf"
  import Motors from "../lib/Motors.lf"
  import GyroAngle from "../lib/IMU.lf"
  import Encoders from "../lib/Encoders.lf"
  import Buttons from "../lib/Buttons.lf"
  
  main reactor(calibration_time: time = 10 s, sample_period: time = 100 ms) {
    preamble {=
    
    =}

    line = new Line()
    disp = new Display()
    motor = new Motors()
    gyro = new GyroAngle()
    encoder = new Encoders()
    buttons = new Buttons();

  
    timer t(0, sample_period)
    timer seconds(0, 1 s)
    timer end_calibration(calibration_time)

    state angle_before_turn: float = 0
    state calibration_time_count: int = 0
    state button_press_time: int = 0

    reaction(t) -> line.trigger, buttons.trigger {=
      lf_set(line.trigger, true);
      lf_set(buttons.trigger, true);
    =}
  
    initial mode MAIN_MENU_MODE {
        //Menu for the Cargo 
        //Uses Buttons to control which mode to be in.
        reaction(t) -> disp.line0, disp.line1, disp.line2, disp.line3 {= 
          static char buf0[17];
          static char buf1[17];
          static char buf2[17];
          static char buf3[17];
          snprintf(buf0, 17, "[A]: Calibration Mode");
          snprintf(buf1, 17, "[B]: DRIVE Mode");
          snprintf(buf2, 17, "[C]: .... Extra");
          snprintf(buf3, 17, "PORT Project");
          lf_set(disp.line0, buf0);
          lf_set(disp.line1, buf1);
          lf_set(disp.line2, buf2);
          lf_set(disp.line3, buf3);
        =}

        //Checking buttons
        reaction(buttons.button_a, buttons.button_b, buttons.button_c) -> CALIBRATION_MODE, DRIVE_MODE, EXTRA_MODE {=
          if (buttons.button_a->value) {
            lf_set_mode(CALIBRATION_MODE);
          } else if (buttons.button_b->value) {
            lf_set_mode(DRIVE_MODE);
          } else if (buttons.button_c->value) {
            lf_set_mode(EXTRA_MODE);
          }
        =}
    }

    mode CALIBRATION_MODE { 
      /* Going back to the main menu */
      reaction(buttons.button_c) -> MAIN_MENU_MODE {=
        if(buttons.button_c->value) {
          lf_set_mode(MAIN_MENU_MODE);
        }
      =}
      /* Entering the Calibration mode and stops it after x amount of seconds*/
      reaction(startup) -> disp.line0, disp.line1, disp.line2 {=
        lf_set(disp.line0, "CALIBRATING");
        lf_set(disp.line1, "Roll robot over");
        lf_set(disp.line2, "light and dark.");
      =}  

      reaction(seconds) -> disp.line3 {=
        if(self->calibration_time_count <= self->calibration_time) {
          static char buf[17];
          snprintf(buf, 17, "time:%8d s", self->calibration_time_count++);
          lf_set(disp.line3, buf);
        }
      =}

      reaction(end_calibration) -> line.calibrate {=
        lf_set(line.calibrate, false);
      =}

      /* After calibration mode, allows for users to see on the LED screen the line encoder values*/
      reaction(line.reflect) -> disp.line0, disp.line1, disp.line2, disp.line3 {=
        static char buf0[17];
        static char buf1[17];
        static char buf2[17];
        static char buf3[17];
        snprintf(buf0, 17, "0:%4d 1:%4d", line.reflect->value[0], line.reflect->value[1]);
        snprintf(buf1, 17, "2:%4d 3:%4d", line.reflect->value[2], line.reflect->value[3]);
        snprintf(buf2, 17, "4:%4d", line.reflect->value[4]);
        snprintf(buf3, 17, "Main Menu: Press [C]");
        lf_set(disp.line0, buf0);
        lf_set(disp.line1, buf1);
        lf_set(disp.line2, buf2);
        lf_set(disp.line2, buf2);
      =}
    }

    mode DRIVE_MODE {
      reaction(line.reflect) gyro.z -> TURN_CORNER, motor.left_power, motor.right_power, disp.line0, disp.line1, disp.line2, disp.line3 {= 
        //Logic for driving
        //Printing to the LED Screen
        static char buf0[17];
        static char buf1[17];
        static char buf2[17];
        static char buf3[17];

        //Robot: Aim to keep the robot moving forward by 
        //using the center line sensor which is line[2] 

        if (line.reflect->value[1] > 700) {
          //Adjusting the robot to the right if the left sensor is detected
          lf_set(motor.left_power, 0.075f);
          lf_set(motor.right_power, 0.1f);
          snprintf(buf0, 17, "Left Sensor Detected");
        } else {
          snprintf(buf0, 17, " ");
        }

        if(line.reflect->value[2] > 800) {
          lf_set(motor.left_power, 0.1f);
          lf_set(motor.right_power, 0.1f);
          snprintf(buf1, 17, "Centered");
        } else {
          snprintf(buf1, 17, " ");
        }

        if (line.reflect->value[3] > 700) {
          //Adjusting the robot to the left if the right sensor is detected
          lf_set(motor.left_power, 0.1f);
          lf_set(motor.right_power, 0.075f);
          snprintf(buf2, 17, "Right Sensor Detected");
        } else {
          snprintf(buf2, 17, " ");
        }

        if (line.reflect->value[4] >= 1000) {
          //When the most rightmost line sensor detects a black line, 
          //this means that there is a turn. We turn 90degrees to the right.
          // lf_set(motor.left_power, 0.15f);
          // lf_set(motor.right_power, 0.1f);'
          lf_set(motor.left_power, 0.20);
          lf_set(motor.right_power, -0.075f);
          self->angle_before_turn = gyro.z->value;
          lf_set_mode(TURN_CORNER);
          snprintf(buf3, 17, "R: %.1f", gyro.z->value);
        }


        if (line.reflect->value[0] >= 1000) {
          //When the most rightmost line sensor detects a black line, 
          //this means that there is a turn. We turn 90degrees to the right.
          // lf_set(motor.left_power, 0.15f);
          // lf_set(motor.right_power, 0.1f);'
          lf_set(motor.left_power, -0.075f);
          lf_set(motor.right_power, 0.20f);
          self->angle_before_turn = gyro.z->value;
          lf_set_mode(TURN_CORNER);
          snprintf(buf3, 17, "D: %.1f", gyro.z->value);
        }


        lf_set(disp.line0, buf0);
        lf_set(disp.line1, buf1);
        lf_set(disp.line2, buf2);
        lf_set(disp.line3, buf3);
      =} 
    }

    mode TURN_CORNER { 
      //Use gyro to check if the turn has been made completely before changing to DRIVE mode again

      reaction(gyro.z) -> DRIVE_MODE, motor.left_power, motor.right_power, disp.line0, disp.line3 {=
        //gyro.z = -90, angle_before_turn = -10, result = -80
        lf_set(disp.line0, "TURN_CORNER Mode");
        if (abs(gyro.z->value - self->angle_before_turn) >= 80) {
          lf_set(motor.left_power, 0.1);
          lf_set(motor.right_power, 0.1);
          lf_set(disp.line3, " ");
          lf_set_mode(DRIVE_MODE);
        }
      =}
    }

    mode STOP_MODE {
      //Temporarily pauses the vehicle. Entered by the DRIVE mode.
    }

    mode EXTRA_MODE {
      //Extra Menus and options for the car
    }
  
  }
  