/**
 * @brief Library reactor for the bump sensors on the
 * <a href="https://www.pololu.com/docs/0J86">Pololu 3pi+ 2040 robot</a>.
 * When this reactor detects a bump on the left or right, it produces a
 * true value on the output.  The period at which it periodically samples the bump
 * sensors is a parameter.  During startup, this reactor auto-calibrates the
 * bump sensors.  It recalibrates them each time it receives a calibrate input.
 *
 * NOTE: The bump sensors cannot be used together with the line sensors.
 * See <a href="https://www.pololu.com/docs/0J86/6.5">Section 6.5 of the Pololu 3pi+ 2040 User's Guide</a>.
 *
 * @author Abhi Gundrala
 * @author Edward A. Lee
 */
target C {
  platform: {
    name: "rp2040",
    board: "pololu_3pi_2040_robot"
  },
  threading: false,
}

preamble {=
    #include <ir_sensors.h>
    // others
=}

reactor Bump(sample_period:time = 100ms) {
    timer t1(0, sample_period);
    input calibrate:bool;
    output left:bool;
    output right:bool;

    reaction(startup, calibrate) {=
        bump_sensors_calibrate();
    =}
    reaction(t1) -> left, right {=
        bump_sensors_read();
        // only is_present when pressed
        if (bump_sensor_left_is_pressed()) {
            lf_set(left, true);
        }
        if (bump_sensor_right_is_pressed()) {
            lf_set(right, true);
        }
    =}

}
